plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()

    maven {
        url = uri("https://maven.pkg.github.com/mariangelhm/qa-core")
        credentials {
            username = project.findProperty("gpr.user")
                    ?: System.getenv("GITHUB_USERNAME")
                    ?: "mariangelhm"

            password = project.findProperty("gpr.key")
                    ?: System.getenv("GITHUB_TOKEN")
        }
        content {
            includeGroup "com.company.qa"   // MUY IMPORTANTE
        }
    }
}


dependencies {

    /* Spring Boot */
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    /* Utilidades */
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.yaml:snakeyaml:2.2'

    /* Web testing */
    implementation 'io.github.bonigarcia:webdrivermanager:5.8.0'
    implementation 'org.seleniumhq.selenium:selenium-java:4.18.1'

    /* Cucumber (NECESARIO EN MAIN PARA STEPS Y HOOKS) */
    implementation 'io.cucumber:cucumber-java:7.15.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.15.0'

    /* Engine solo para test */
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.15.0'

    /* QA CORE desde GitHub Packages */
    implementation 'com.company.qa:qa-core:1.0.3'

    /* TEST */
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

task runCucumber(type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'io.cucumber.core.cli.Main'

    def tags = project.findProperty('cucumber.tags') ?: ''

    args = [
            '--plugin', 'pretty',
            '--plugin', 'json:build/cucumber.json',
            '--plugin', 'junit:build/cucumber.xml',
            '--glue', 'com.example.webtestingia.steps',
            '--glue', 'com.example.webtestingia.hooks',
            'src/test/resources/features'
    ]

    if (tags) {
        args += ['--tags', tags]
    }
}
